/** Identify herb and give experience. */
[proc,identify_herb](namedobj $identified_herb, int $slot)
def_int $experience = oc_param($identified_herb, identified_herb_exp);
def_string $message = oc_param($identified_herb, identified_herb_message);
inv_setslot(inv, $slot, $identified_herb, 1);
mes($message); // https://youtu.be/RVnSlXYGPe0?t=72
stat_advance(herblore, $experience);

/** Brew a potion. */
[proc,brew_potion](struct $struct, namedobj $ingredient, namedobj $solvent, string $message, namedobj $mixture, int $experience)
if (getqueue(brew_potion_repeat) > 0) {
  clearqueue(brew_potion_repeat);
}
inv_del(inv, $ingredient, 1);
inv_del(inv, $solvent, 1);
// There is always a guaranteed slot available.
inv_add(inv, $mixture, 1);
// https://youtu.be/S6P-hUzgqPw?t=1
mes($message);
anim(human_herbing_vial, 0);
sound_synth(grind, 0, 0);
stat_advance(herblore, $experience);
queue(brew_potion_repeat, 2, $struct, $ingredient, $solvent, $message, $mixture, $experience);

[queue,brew_potion_repeat](struct $struct, namedobj $ingredient, namedobj $solvent, string $message, namedobj $mixture, int $experience)
if (inv_total(inv, $ingredient) > 0 & inv_total(inv, $solvent) > 0) {
  ~brew_potion($struct, $ingredient, $solvent, $message, $mixture, $experience);
}

/** Grind a herblore ingredient. */
[proc,grind_ingredient](obj $grindable, namedobj $refined, string $message, int $slot)
if (getqueue(grind_ingredient_repeat) > 0) {
  clearqueue(grind_ingredient_repeat);
}
inv_setslot(inv, $slot, $refined, 1);
// https://youtu.be/QUWaqEBTrCE?t=107
mes($message);
anim(human_herbing_grind, 0);
sound_synth(grind, 0, 0);
queue(grind_ingredient_repeat, 2, $grindable, $refined, $message);

[queue,grind_ingredient_repeat](obj $grindable, namedobj $refined, string $message)
if (inv_total(inv, $grindable) > 0) {
  def_int $slot = ~inv_find(inv, $grindable, 0);
  if ($slot >= 0) {
    ~grind_ingredient($grindable, $refined, $message, $slot);
  }
}
