[opheldu,bankers_note]

def_obj $item_to_process;
def_int $item_slot;

// Determine which item is the note and which is being processed
if (last_item = bankers_note) {
    // Used other item ON the note
    $item_to_process = last_useitem;
    $item_slot = last_useslot;
} else {
    // Used the note ON other item
    $item_to_process = last_item;
    $item_slot = last_slot;
}

// Check if accidentally used note on itself
if ($item_to_process = bankers_note) {
    mes("Nothing interesting happens.");
    return;
}

// Get ObjType info for the item being processed
def_obj $noted_version = oc_cert($item_to_process);
def_obj $unnoted_version = oc_uncert($item_to_process);

// Check if the item is noteable/unnoteable at all
if ($noted_version = $unnoted_version) {
    mes("This item cannot be noted or unnoted.");
    return;
}

if ($item_to_process = $unnoted_version) {
    inv_moveitem_cert(inv, inv, $unnoted_version, 1);

    while(inv_total(inv, $unnoted_version) > 0) {
        inv_moveitem_cert(inv, inv, $unnoted_version, 1);
    }
    return;
}

if ($item_to_process = $noted_version) {
    if (inv_freespace(inv) = 0 & inv_total(inv, $noted_version) > 1) {
        mes("You don't have enough room in your inventory.");
        return;
    }

    if (inv_total(inv, $noted_version) = calc(inv_freespace(inv) + 1)) {
        inv_moveitem_uncert(inv, inv, $noted_version, inv_total(inv, $noted_version));
        return;
    }

    if (inv_freespace(inv) < inv_total(inv, $noted_version)) {
        inv_moveitem_uncert(inv, inv, $noted_version, inv_freespace(inv));
        return;
    }

    inv_moveitem_uncert(inv, inv, $noted_version, inv_total(inv, $noted_version));
    return;
}
